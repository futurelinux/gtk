pkgname=wxgtk2.8
pkgver=2.8.12.1
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'sdl')
makedepends=('gstreamer0.10-base-plugins' 'glu')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2
        'wxGTK-collision.patch'
        'make-abicheck-non-fatal.patch')
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5'
          '575fb4688051c246777988332f55703df403d6b9'
          'dfe38650c655395b90bf082b5734c4093508bfa3')

prepare() {
  cd wx*-${pkgver}
  patch -p1 -i ../wxGTK-collision.patch
  
  # C++ ABI check is too strict and breaks with GCC 5.1
  # https://bugzilla.redhat.com/show_bug.cgi?id=1200611
  patch -Np1 -i ../make-abicheck-non-fatal.patch
}

build() {
  cd wx*-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx  --disable-optimize --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-sdl --disable-precomp-headers --without-gconf
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  cd wx*-${pkgver}
  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
