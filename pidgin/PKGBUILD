pkgname=('pidgin')
pkgver=2.13.0
pkgrel=4
arch=('x86_64')
pkgdesc="Multi-protocol instant messaging client"
url="http://pidgin.im/"
license=('GPL')
depends=('libpurple' 'startup-notification' 'gtkspell' 'libxss' 'libsm'
         'hicolor-icon-theme' 'farstream')
makedepends=('nss' 'libsasl' 'libidn' 'python3' 'avahi'
             'tk' 'ca-certificates' 'intltool' 'networkmanager' 'libidn')
optdepends=('aspell: for spelling correction')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'purple-remote-python3.patch')
md5sums=('423403494fe1951e47cc75231f743bb0'
         '0709677de2cb5bc08b30e6b04ea0d3c5')

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   patch -Np1 -i ../purple-remote-python3.patch
   autoreconf -vi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-meanwhile \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python3 \
    --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C pidgin DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES
}

