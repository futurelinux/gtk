pkgname=wxmaxima
pkgver=19.05.2
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('x86_64')
url="https://wxmaxima-developers.github.io/wxmaxima/"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk3') # 'ttf-linux-libertine' required?
makedepends=('cmake' 'doxygen')
source=($pkgname-$pkgver.tar.gz::"https://github.com/wxMaxima-developers/wxmaxima/archive/Version-${pkgver}.tar.gz")
sha1sums=('34bc74b46f6a67bd357b777e5d0df1b01d661abb')

build() {
  cd "${srcdir}/${pkgname}-Version-${pkgver}"
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd "${srcdir}/${pkgname}-Version-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
