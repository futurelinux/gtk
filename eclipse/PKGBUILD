pkgbase=eclipse
pkgname=(eclipse-{common,java,jee,cpp,php,javascript})
pkgver=4.14
pkgrel=1
_release=2019-12
pkgdesc="Highly extensible IDE"
license=(EPL)
arch=(x86_64)
url="https://eclipse.org"
makedepends=(python3)
options=(!emptydirs)
source=(commonify)
noextract=()

for _pkg in ${pkgname[@]}; do
  [[ $_pkg == 'eclipse-common' ]] && continue
  source+=(http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/$_release/R/$_pkg-$_release-R-linux-gtk-x86_64.tar.gz)
  noextract+=($_pkg-$_release-R-linux-gtk-x86_64.tar.gz)
  eval "package_$_pkg() { _package $_pkg; }"
done

sha256sums=('da1eaf6239cb0c2b5009cd8c261e96e7b17a7536b12fe60fcb1e903f5e67dbdc'
            'eea78b2daab7c5d3e11cd86d0782023a05f7e173265f470536357ef97cd31a29'
            'a012ad5dd54c344aff66b82f0ed37b54002e727566c7d786d0d60f96b1117c38'
            '56c3f4757d31ac002f996de0e81edb18cdf660d6193aa0b076e25dae32079789'
            '427b1d74485ccde8b3c6d2b23390be74123281e90ad8e93db701d9835130039a'
            '2a17772438a1966f182e2aa2433e1119c9988b2d50f54dc89f476fcca8fe2755')

prepare() {
  local pkg
  for pkg in ${pkgname[@]}; do
    mkdir $pkg
    [[ $pkg == eclipse-common ]] && continue
    bsdtar -xf "$pkg-$_release-R-linux-gtk-x86_64.tar.gz" -C $pkg --strip-components 1
  done
}

build() {
  mkdir eclipse-common/dropins
  touch eclipse-common/dropins/.keep
  ./commonify --identical ${pkgname[@]}
}

package_eclipse-common() {
  pkgdesc+=" (common files)"
  depends=("java-environment>=8" webkit2gtk unzip)

  install -d "$pkgdir/usr/lib"
  cp -a eclipse-common "$pkgdir/usr/lib/eclipse"

  install -D /dev/stdin "$pkgdir/usr/bin/eclipse" <<END
#!/bin/bash
export ECLIPSE_HOME=/usr/lib/eclipse
exec \$ECLIPSE_HOME/eclipse "\$@"
END

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/eclipse.desktop" <<END
[Desktop Entry]
Name=Eclipse
Comment=A Java Development Environment
Icon=eclipse
Exec=eclipse
Terminal=false
Type=Application
Categories=Development;IDE;Java;
StartupNotify=true
END

  local i
  for i in 16 32 48 256; do
    install -Dm644 eclipse-common/plugins/org.eclipse.platform_*/eclipse$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x$i/apps/eclipse.png"
  done
}

_package() {
  local variant=${1#eclipse-}
  pkgdesc+=" (${variant^^} variant)"
  depends=(eclipse-common)
  provides=("eclipse=$pkgver-$pkgrel")
  conflicts=(eclipse)

  case $variant in
    java) replaces=(eclipse) ;;
    cpp)  replaces=(eclipse-cdt) ;;
  esac

  install -d "$pkgdir/usr/lib"
  cp -a $1 "$pkgdir/usr/lib/eclipse"
}

