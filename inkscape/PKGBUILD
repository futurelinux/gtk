# Maintainer: Jeff Hunag <s8321414[at]gmail[dot]com>

pkgname=inkscape
pkgver=0.91
pkgrel=5
pkgdesc='Vector graphics editor using the SVG file format'
url='http://inkscape.sourceforge.net/'
license=('GPL' 'LGPL')
arch=('x86_64')
makedepends=('boost' 'intltool')
depends=('gc' 'gsl' 'gtkmm' 'gtkspell' 'imagemagick' 'libxslt' 'poppler' 'poppler-glib' 'popt'
         'python2' 'desktop-file-utils' 'hicolor-icon-theme' 'fftw')
optdepends=('pstoedit: latex formulas'
            'texlive-core: latex formulas'
            'python2-numpy: some extensions'
            'python2-lxml: some extensions and filters'
            'pyxml: some extensions'
            'uniconvertor: reading/writing to some proprietary formats')
#source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
source=("https://inkscape.global.ssl.fastly.net/media/resources/file/$pkgname-$pkgver.tar.bz2" inkscape_zh_TW.mo)
sha1sums=('9941ee467af570ac71a70f965cd9c48d4993b8f3'
          '78f10b7afa35a888fd31ed00997d355c3c019882')
install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
	sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
	sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
	sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
	sed -i 's|"python"|"python2"|g' src/main.cpp
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--with-python \
		--with-perl \
		--enable-lcms \
		--enable-poppler-cairo \
		--disable-dependency-tracking
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Workaround for 0.91 Taiwanese mo bug
	install -Dm644 ${srcdir}/inkscape_zh_TW.mo ${pkgdir}/usr/share/locale/zh_TW/LC_MESSAGES/inkscape.mo
}
