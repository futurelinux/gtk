# Contributions from Arch:
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.0
_commit=4035a4fb499b37632e8788731a2d3425d5a6bc2a
pkgrel=1
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
depends=('gc' 'gsl' 'gtkmm3' 'gtkspell3' 'libmagick6' 'libxslt' 'poppler-glib'
	 'potrace' 'ttf-dejavu' 'python3' 'libcdr' 'gdl'
         'dbus-glib' 'double-conversion' 'libjpeg-turbo' 'libvisio' 'libsoup')
makedepends=('boost' 'cmake' 'git')
optdepends=('pstoedit: latex formulas'
            'texlive-core: latex formulas'
            'python3-numpy: some extensions'
            'python3-lxml: some extensions'
            'gvfs: import clip art'
            'scour: optimized SVG output, some extensions')
source=("git+https://gitlab.com/inkscape/inkscape.git#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
	cd "${srcdir}"
	cd "${pkgname}"
	git submodule init
	git submodule update
}

build() {
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'

	cmake -H${pkgname} -Bbuild \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DINCLUDE_INSTALL_DIR:PATH=/usr/include \
		-DLIB_INSTALL_DIR:PATH=/usr/lib \
		-DSYSCONF_INSTALL_DIR:PATH=/etc \
		-DSHARE_INSTALL_PREFIX:PATH=/usr/share
	cmake --build build
}

package() {
	DESTDIR=$pkgdir cmake --build build --target install
}
