# WARNING: this package should exactly match the hugin-tools platform package

pkgname=hugin
pkgver=2017.0.0
pkgrel=2
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('x86_64')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' "hugin-tools=${pkgver}"
         'lensfun' 'lapack' 'make' 'perl-exiftool' 'desktop-file-utils' 'glew')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'zip' 'swig')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('4ba7f5bf5afdadbea9cf10d15aa14e644ef94ed3')

install=install

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix compiling against lensfun-0.3.0
  sed '/LF_DIST_MODEL_FOV1/d' -i src/hugin_base/lensdb/LensDB.cpp
}

build() {
    cd ${pkgname}-${pkgver}

    mkdir -p build 
    cd build

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
        -DENABLE_LAPACK=yes \
        -DBUILD_HSI=no
        make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR="${pkgdir}" install

  msg "Removing duplicated binaries in hugin-tools"
  rm -v "${pkgdir}/usr/share/hugin/data/celeste.model"
  rm -v "${pkgdir}/usr/bin/autooptimiser"
  rm -v "${pkgdir}/usr/bin/hugin_hdrmerge"
  rm -v "${pkgdir}/usr/bin/linefind"
  rm -v "${pkgdir}/usr/bin/nona"
  rm -v "${pkgdir}/usr/bin/fulla"
  rm -v "${pkgdir}/usr/bin/align_image_stack"
  rm -v "${pkgdir}/usr/bin/celeste_standalone"
  rm -v "${pkgdir}/usr/bin/vig_optimize"
  rm -v "${pkgdir}/usr/bin/cpfind"
  rm -v "${pkgdir}/usr/bin/tca_correct"
  rm -v "${pkgdir}/usr/bin/pano_trafo"
  rm -v "${pkgdir}/usr/bin/pano_modify"
  rm -v "${pkgdir}/usr/bin/pto_gen"
  rm -v "${pkgdir}/usr/bin/deghosting_mask"
  rm -v "${pkgdir}/usr/bin/pto_merge"
  rm -v "${pkgdir}/usr/bin/checkpto"
  rm -v "${pkgdir}/usr/bin/cpclean"
  rm -v "${pkgdir}/usr/bin/geocpset"
  rm -v "${pkgdir}/usr/bin/pto_lensstack"
  rm -v "${pkgdir}/usr/bin/pto_var"
  rm -v "${pkgdir}/usr/bin/pto_move"
  rm -v "${pkgdir}/usr/bin/pto_mask"
  rm -v "${pkgdir}/usr/bin/pto_template"
  rm -v "${pkgdir}/usr/bin/hugin_lensdb"
  rm -v "${pkgdir}/usr/bin/verdandi"
 
  rm -rfv "${pkgdir}/usr/share/appdata"
  rm -v "${pkgdir}/usr/share/hugin/data/hugin_exiftool_copy.arg"

  msg "Removing hugin-tools libs"
  cd "${pkgdir}/usr/lib/hugin/"
  rm "${pkgdir}/usr/lib/hugin/libhuginbase.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/liblocalfeatures.so.0.0"
  rm "${pkgdir}/usr/lib/hugin/libceleste.so.0.0"
}
