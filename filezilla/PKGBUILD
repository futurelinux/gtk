pkgname=filezilla
pkgver=3.43.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('x86_64')
url="https://filezilla-project.org/"
license=('GPL')
depends=('dbus' 'xdg-utils' 'wxgtk3' 'libidn' 'hicolor-icon-theme' 'sqlite3' 'gnutls' 'libfilezilla' 'libnotify')
source=("https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2")
sha256sums=('6fe652c360682f860abe2dc2e196bddca6260242cefe6fe779d42c327c01258e')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin \
    --with-wx-config=/usr/bin/wx-config-gtk3

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
