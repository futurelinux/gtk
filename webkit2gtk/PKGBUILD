# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/webkitgtk
# Contributor on CCR: Ben R <thebenj88 *AT* gmail *DOT* com>

pkgname=webkit2gtk
pkgver=2.26.1
pkgrel=2
pkgdesc="GTK+ Web content engine library"
arch=('x86_64')
url="https://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'geoclue' 'sqlite' 'icu' 'gst-plugins-base-libs' 'gst-plugins-bad' 'libsecret'
         'libsoup' 'enchant' 'libwebp' 'harfbuzz-icu' libnotify hyphen woff2 libwpe wpebackend-fdo
         bubblewrap xdg-dbus-proxy gtk3)
makedepends=('gperf' 'gobject-introspection' 'python3' 'ruby' 'ninja' 'cmake' gtk-doc)
provides=('libwebkit')
options=('!emptydirs')
conflicts=(libwebkit-gtk2)
replaces=(libwebkit-gtk2)
provides=(libwebkit-gtk2)
source=(https://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz{,.asc})
sha256sums=('6b4b21801d2b1008422a1075dbd6fb4ae8b5127503faf657cf9671289d9cd155'
            'SKIP')
validpgpkeys=('D7FCF61CF9A2DEAB31D81BD3F3D322D0EC4582C3')

build() {
  cmake -Hwebkitgtk-$pkgver -Bbuild \
    -DPORT=GTK \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DENABLE_GTKDOC=OFF \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DLIB_INSTALL_DIR=/usr/lib
  cmake --build build
}

check() {
  : cmake --build build --target tests
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install

  cd webkitgtk-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
