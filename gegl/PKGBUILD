pkgname=gegl
pkgver=0.4.20
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('x86_64')
url="https://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'libspiro' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'luajit' 'jasper' 'lensfun' 'suitesparse')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'vala' 'gobject-introspection' 'git' 'meson' 'python3-gobject')
optdepends=('ffmpeg: for using the ffmpeg plugin')
_commit=ef8b69c06d1c4c0502a09f18bd70c5c6effe587b  # tags/GEGL_0_4_20^0
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GEGL_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir -p build
  cd $pkgname
  git cherry-pick -n c1c3541caab485384087bbbcf1c83ef2d5bd4d43
}

build() {
  export LC_ALL=en_US.UTF-8
  cd build
  chakra-meson ../${pkgname} \
    -Dworkshop=true \
    -Dmrg=disabled
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
