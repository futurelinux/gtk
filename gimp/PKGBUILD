pkgname=gimp
pkgver=2.10.12
_pkgver=2.10
pkgrel=1
pkgdesc="GNU Image Manipulation Program (Development Version)"
arch=('x86_64')
url="https://www.gimp.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'dbus-glib' 'desktop-file-utils' 'gegl' 'glib-networking' 'hicolor-icon-theme'
         'jasper' 'lcms2' 'libheif' 'libexif' 'libgudev' 'libmng' 'libmypaint' 'librsvg' 'libwebp'
         'libwmf' 'libxmu' 'libxpm' 'mypaint-brushes' 'openexr' 'poppler-data' 'pygtk')
makedepends=('alsa-lib' 'curl' 'ghostscript' 'gtk-doc' 'intltool' 'iso-codes' 'poppler-glib')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support'
            'ffmpeg')
source=("https://download.gimp.org/pub/gimp/v${_pkgver}/$pkgname-$pkgver.tar.bz2" "linux.gpl")
md5sums=('4cb0a17ea885ad197b5921c78d83456a'
         'bb27bc214261d36484093e857f015f38')

prepare() {
  cd $pkgname-$pkgver
  _mypaintver=$(echo /usr/lib/libmypaint-*.so | grep -o -E '\-[0-9]+(\.[0-9]+)*' | head -1)
  sed -i "s|\\(libmypaint\\)\\( >= libmypaint_required_version\\)|\\1${_mypaintver}\\2|g" configure.ac
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure \
    --prefix=/usr --sysconfdir=/etc --libexec=/usr/bin \
    --enable-mp \
    --enable-gimp-console \
    --enable-python \
    --with-gimpdir=.config/gimp \
    --with-bug-report-url='https://code.chakralinux.org/packages/gtk/issues/?search=gimp' \
    --without-aa \
    --with-openexr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 ../linux.gpl $pkgdir/usr/share/gimp/2.0/palettes/Linux.gpl

  ln -sf gimp-console-$pkgver.1.gz $pkgdir/usr/share/man/man1/gimp-console.1.gz
  ln -sf gimprc-$pkgver.5.gz $pkgdir/usr/share/man/man5/gimprc.5.gz
  ln -sf gimptool-2.0.1.gz $pkgdir/usr/share/man/man1/gimptool.1.gz
  ln -s gimp-$pkgver.1.gz $pkgdir/usr/share/man/man1/gimp.1.gz
}
