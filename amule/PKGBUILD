pkgname=amule
pkgver=11004
pkgrel=1
epoch=1
pkgdesc="An eMule-like client for ed2k p2p network"
arch=('x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('wxgtk2' 'gd' 'geoip' 'libupnp' 'crypto++' 'boost-libs')
makedepends=('ccache' 'git' 'boost')
source=("git://repo.or.cz/amule.git#tag=amule-svn-r$pkgver"
        'amuled.systemd'
        'amuleweb.systemd'
        'amule.sysusers'
        'amule.tmpfiles'
        'amule-wxgtk-3.1.patch')
sha256sums=('SKIP'
            '20ac6b60c5f3bf49c0b080dfc02409da3c9d01b154344188008c6a75ca69681e'
            'f4f43b1154ddccc9036a4291a58c6715f097b171fec62ea7aead0c9d9fa654f2'
            'c4ca658ab4105b3b90e0bb3efcc8121eca1c4d873787db1ed4f637925c16d502'
            'e9d1b7019c7075b0f8616c6507a767b87de8f899936680e9ff5829d8cbba224d'
            '2486059f4358e6c3cc1b42c8ca2494dca5e86321663ac13c3d2a8f4c1fe23fd2')

prepare(){
  cd $pkgname
  patch -p1 -i ../amule-wxgtk-3.1.patch # Fix build with wxgtk 3.1
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-ccache \
              --enable-geoip \
              --enable-upnp \
              --enable-fileview \
              --with-boost
  make
}

package() {
  cd $pkgname

  make DESTDIR=$pkgdir install

  install -Dm644 "$srcdir"/amuled.systemd "$pkgdir"/usr/lib/systemd/system/amuled.service
  install -Dm644 "$srcdir"/amuleweb.systemd "$pkgdir"/usr/lib/systemd/system/amuleweb.service
  install -Dm644 "$srcdir"/amule.sysusers "$pkgdir"/usr/lib/sysusers.d/amule.conf
  install -Dm644 "$srcdir"/amule.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/amule.conf
}
