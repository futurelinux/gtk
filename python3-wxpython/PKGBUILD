# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python3-wxpython
_pkgname=wxPython
pkgver=4.0.4
pkgrel=1
pkgdesc="Phoenix wxWidgets GUI toolkit for Python"
arch=('x86_64')
license=('custom:wxWindows')
url="https://www.wxpython.org"
depends=('wxgtk3' 'python3-setuptools' 'python3-six') #'python3-pypubsub')
makedepends=('mesa' 'glu' 'libwebkit-gtk2')
checkdepends=('xorg-server-xvfb' 'python3-pytest' 'python3-numpy')
source=("https://files.pythonhosted.org/packages/source/w/wxPython/wxPython-${pkgver}.tar.gz")
sha512sums=('48895f191150c63c253eb4136b78ff7803b6df566ca92b9fcf3c3fff3c893a57ff52a51b331333cf2200a053b8d0ea7facb39d7bfb581a78e009a24c31437cae')

prepare() {
  sed -i "s|WX_CONFIG = 'wx-config'|WX_CONFIG = 'wx-config-gtk3'|" $_pkgname-$pkgver/build.py
}

build() {
  cd $_pkgname-$pkgver

  python3 build.py build --use_syswx --release
}

check() {
  cd $_pkgname-$pkgver

  xvfb-run python3 build.py test
}

package() {
  cd $_pkgname-$pkgver

  python3 build.py install --destdir="$pkgdir"

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  find "$pkgdir/usr/lib" -type f | xargs chmod 644
}

